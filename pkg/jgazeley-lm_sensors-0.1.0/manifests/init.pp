# Module only for physical machines that sets up lm_sensors
class lm_sensors {
  if $::virtual == 'physical' {
    # Path to config file
    $config = $::osfamily ? {
      'RedHat' => '/etc/sysconfig/lm_sensors',
      'Debian' => '/etc/modprobe.d/lm_sensors.conf',
    }

    # Name of service/package
    $package = $::osfamily ? {
      'RedHat' => 'lm_sensors',
      'Debian' => 'lm-sensors',
    }

    # Install lm_sensors
    package { 'lm_sensors':
      name   => $package,
      ensure => installed,
      notify => Exec['sensors-detect'],
    }

    # Scan for hardware sensors
    exec { 'sensors-detect':
      command => '/usr/sbin/sensors-detect < /dev/null',
      unless  => "/bin/grep \"Generated by sensors-detect\" $config",
      notify  => Service['lm_sensors'],
      require => Package['lm_sensors'],
    }

    # Start lm_sensors service
    service { 'lm_sensors':
      name       => $package,
      enable     => true,
      ensure     => running,
      hasrestart => true,
      require    => [
        Package['lm_sensors'],
        Exec['sensors-detect'],
      ],
    }
  }
}
